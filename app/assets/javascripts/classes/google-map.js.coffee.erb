class @GoogleMap
  # the google maps api v3 must be loaded prior to this

  constructor: (@html) ->
    @activePopup = undefined
    @markers = @popups = []
    @mapID = @html.data('map-id')
    @icons =
      red: "<%= asset_path 'marker-red.png' %>"
      gray: "<%= asset_path 'marker-gray.png' %>"

    # create map
    map_element = $('<div class="map-frame">').appendTo(@html).get(0)
    @map = new (google.maps.Map)(map_element,
      center: new (google.maps.LatLng)(parseFloat(@html.data('lat')), parseFloat(@html.data('lon')))
      zoom: parseInt(@html.data('zoom'))
      minZoom: 11
      maxZoom: 17
      zoomControl: true
      gestureHandling: 'none'
      disableDefaultUI: true
      styles: [
        {featureType: 'poi', elementType: 'labels', stylers: [visibility: 'off']},
        {featureType: 'transit', stylers: [visibility: 'off']},
        {featureType: 'road', elementType: 'labels.icon', stylers: [visibility: 'off']}
      ]
    )

    # add markers
    $('.markers > *', @html).each (i, ele) =>
      m = $(ele)
      @addMarker(m.data('name') or '', m.data('href') or '', m.prop('innerHTML'),
          parseFloat(m.data('lat')), parseFloat(m.data('lon')), m.data('color'))
      if m.data('editable')
        @setEditable(m.data('name'), @mapID)
    @adaptBoundsToMarkers




  addMarker: (name, href, desc, lat, lon, color) =>
    marker = new (google.maps.Marker)
      title: name
      map: @map
      position: new (google.maps.LatLng)(lat, lon)
      icon: @icons[color]
    @markers.push marker

    popup = new (google.maps.InfoWindow)
      maxWidth: 300
      content: '<a href="' + href + '"><h4>' + name + '</h4></a>' + desc
    @popups.push popup

    google.maps.event.addListener marker, 'click', =>
      if @activePopup
        @activePopup.close()
      popup.open @map, marker
      @activePopup = popup


  removeMarkers: =>
    m.setMap(null) for m in @markers
    w.setMap(null) for w in @popups
    @markers = @popups = []


  adaptBoundsToMarkers: =>
    if @markers.length == 0
      return
    bounds = new (google.maps.LatLngBounds)
    for marker of @markers
      bounds.extend marker.position
    @map.fitBounds bounds


  setEditable: (title, editID) =>
    google.maps.event.addListener @map, 'click', (e) =>
      lat = e.latLng.lat()
      lon = e.latLng.lng()
      @removeMarkers()
      @addMarker title, '', '', lat, lon, 'red'
      $('#' + editID + '_latitude').val lat
      $('#' + editID + '_longitude').val lon
    google.maps.event.addListener @map, 'center_changed', (e) =>
      center = @map.getCenter()
      $('#' + editID + '_map_latitude').val center.lat()
      $('#' + editID + '_map_longitude').val center.lng()
    google.maps.event.addListener @map, 'zoom_changed', =>
      $('#' + editID + '_map_zoom').val @map.getZoom()